apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.2
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from 'build/docs/groovydoc'
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

tasks.uploadArchives.dependsOn javadocJar
tasks.uploadArchives.dependsOn sourcesJar

def isReleaseVersion = !version.endsWith('SNAPSHOT')

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (isReleaseVersion) {
                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            } else {
                repository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.project {
                name 'Gradle SkinnyWar'
                packaging 'jar'
                description 'Gradle SkinnyWar is a Gradle plugin to provide skinny war functionality'
                url 'https://github.com/litpho/gradle-skinnywar'

                scm {
                    url 'scm:git@github.com:litpho/gradle-skinnywar.git'
                    connection 'scm:git@github.com:litpho/gradle-skinnywar.git'
                    developerConnection 'scm:git@github.com:litpho/gradle-skinnywar.git'
                }

                licenses {
                    license {
                        name "The MIT License (MIT)"
                        url "http://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id "litpho"
                        name "Jasper de Vries"
                        email "litpho@gmail.com"
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}
